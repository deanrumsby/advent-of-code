# Day 4: The Ideal Stocking Stuffer

```elixir
Mix.install([
  {:kino, "~> 0.12.3"}
])
```

## Input

```elixir
input = Kino.Input.textarea("Please paste your puzzle input:")
```

## Shared Modules

`AdventCoinMiner` takes a secret and allows us to mine for successful hashes. It does so by creating an infinite stream of `{index, hash}` tuples, which is then enumerated to find the first hash that satisfies the given difficulty condition.

````elixir
defmodule AdventCoinMiner do
  @doc """
  Finds the first successful index for mining, given the secret and difficulty. 

  ## Example 
  ```elixir
  iex> AdventCoinMiner.mine("hello", 2)
  33
  ```
  """
  def mine(secret, difficulty) do
    Stream.unfold(1, &{hash(secret, &1), &1 + 1})
    |> Enum.find(&success?(&1, difficulty))
    |> elem(0)
  end

  # returns a tuple {index, hash} using the md5 hashing algorithm
  defp hash(secret, index) do
    create_nonce(secret, index)
    |> then(fn nonce -> Base.encode16(:crypto.hash(:md5, nonce)) end)
    |> then(&{index, &1})
  end

  # checks the first n (difficulty) characters of the hash for zeroes
  defp success?(hash, difficulty) do
    String.starts_with?(elem(hash, 1), String.duplicate("0", difficulty))
  end

  # concatenates the secret and current index
  defp create_nonce(secret, index), do: secret <> Integer.to_string(index)
end
````

## Part 1

We use `AdventCoinMiner.mine/2` with the secret and set difficulty to find the required index.

```elixir
input
|> Kino.Input.read()
|> AdventCoinMiner.mine(5)
```

## Part 2

Exactly the same process as part 1, using a higher difficulty condition.

```elixir
input
|> Kino.Input.read()
|> AdventCoinMiner.mine(6)
```
