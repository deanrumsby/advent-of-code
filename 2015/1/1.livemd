# Day 1: Not Quite Lisp

```elixir
Mix.install([
  {:kino, "~> 0.12.0"}
])
```

## Input

```elixir
input = Kino.Input.textarea("Please paste your puzzle input:")
```

## Part 1

To determine the floor Santa arrives at, we just need to iterate through all of the instructions whilst keeping count of the current floor number.

```elixir
input
|> Kino.Input.read()
|> String.graphemes()
|> Enum.reduce(0, fn
  "(", floor -> floor + 1
  ")", floor -> floor - 1
end)
```

## Part 2

To find the position at which Santa enters the basement, we iterate again through all of the instructions, but this time we keep count of both the current floor number and the position. 
As soon as Santa enters the basement we can halt the iteration and return the position.

```elixir
input
|> Kino.Input.read()
|> String.graphemes()
|> Enum.reduce_while({0, 0}, fn
  _, {-1, position} -> {:halt, position}
  "(", {floor, position} -> {:cont, {floor + 1, position + 1}}
  ")", {floor, position} -> {:cont, {floor - 1, position + 1}}
end)
```
